import { Callout } from 'nextra-theme-docs'
import { Tab, Tabs } from 'nextra-theme-docs'

# Creating Endpoints

Now that we are confident with our smart contract, it's time that we make it accessible to end user.

The approach here would be that front-end will first ask us for the unsigned transaction body by giving us the relevant parameters needed to construct it. Then front-end will get this transaction body signed in browser to obtain signed transaction body which it will then submit either via browser wallet's endpoint or via calling the submit endpoint which we will define here.

We'll use [Servant](https://docs.servant.dev/en/stable/) to create our endpoints and one may understand it by following their easy to understand tutorial [here](https://docs.servant.dev/en/stable/tutorial/index.html).

<Callout>
Do note that we can also sign the transactions in server using the `signTx` function defined in [`TxBody.hs`](https://github.com/geniusyield/atlas/tree/main/src/GeniusYield/Types/TxBody.hs)
</Callout>

## Providing Data Provider

### Defining Provider Configuration

As noted earlier, building transaction bodies require gathering suitable information from the blockchain. For this purpose, we'll require a provider. Atlas is unopinionated and allows user to plug in provider of their choice, including a locally hosted one.

Currently Atlas supports the following providers (& it would be highly appreciated if community enriches this by contributing to [Atlas](https://github.com/geniusyield/atlas/tree/main)):

* [Maestro](https://www.gomaestro.org/).
* Locally ran node.
* Cardano DB Sync (but only for private network tests).

To provide information about the provider, we will create a `config.json` file whose contents could be as follows:

<Callout>
  We have given a sample `config.json` file [here](https://github.com/geniusyield/atlas-examples/tree/main/bet-ref/config.sample.json).
</Callout>

<Tabs items={['Maestro', 'Local Node']}>
  <Tab>
    ```json
    {
      "coreProvider": { "maestroToken": "<Your-API-Key>" },
      "networkId": "testnet-preprod",
      "logging": [{ "type": { "tag": "stderr" }, "severity": "Debug", "verbosity": "V2" }],
      "utxoCacheEnable": false
    }
    ```
  </Tab>
  <Tab>
    ```json
    {
      "coreProvider": { "socketPath": "<Path-To-node.socket-File>", "maestroToken": "<Your-API-Key>" },
      "networkId": "testnet-preprod",
      "logging": [{ "type": { "tag": "stderr" }, "severity": "Debug", "verbosity": "V2" }],
      "utxoCacheEnable": false
    }
    ```
  </Tab>
</Tabs>

Here is the explaination for each of the JSON keys above:

* **`coreProvider`**: This field is the differentiating factor between different providers. Above we have given how it would look like for locally ran node & Maestro. Note that local node option still requires Maestro key for `lookupDatum` query.
* **`networkId`**: Specifies your network and must be one of `mainnet`, `testnet-preprod`, `testnet-preview`, `testnet` (for legacy testnet) & `privnet` (for local private network).
* **`logging`**: It's a list of [scribes](https://hackage.haskell.org/package/katip-0.8.7.3/docs/Katip.html#t:Scribe) to register. Its parameters (like `severity`, `verbosity`) and its general usage can be understood by going over their official haddock documentation [here](https://hackage.haskell.org/package/katip-0.8.7.2/docs/Katip.html). Katip is also explained in [this](https://link.springer.com/book/10.1007/978-1-4842-3739-7) book on web development in Haskell.
* **`utxoCacheEnable`**: Enabling this boolean will enable cache (using [`Data.Cache`](https://hackage.haskell.org/package/cache-0.1.3.0/docs/Data-Cache.html)) whereby queries related to fetching UTxOs won't generate call to provider if the entry exists in cache (& has not yet expired).

### Parsing Given Configuration

The file [`server-main.hs`](https://github.com/geniusyield/atlas-examples/tree/main/bet-ref/server/server-main.hs) fires up our server. It reads & parses the configuration file and using it makes our endpoints (which we will define shortly) available. Here is its entire code.

```haskell showLineNumbers {1-7, 14-19}
-- | Getting path for our core configuration.
parseArgs :: IO FilePath
parseArgs = do
  args <- getArgs
  case args of
    coreCfg: _ -> return coreCfg
    _invalidArgument -> fail "Error: wrong arguments, needed a path to the CoreConfig JSON configuration file\n"

main :: IO ()
main = do
  putStrLn "Writing Swagger file ..."
  BL8.writeFile "swagger-api.json" (encodePretty apiSwagger)

  putStrLn "parsing Config ..."
  coreCfgPath <- parseArgs
  coreCfg <- coreConfigIO coreCfgPath  -- Parsing our core configuration.

  putStrLn "Loading Providers ..."
  withCfgProviders coreCfg "api-server"  $ \providers -> do
    let port = 8081
        ctx = Ctx coreCfg providers
    putStrLn $ "Starting server at \n " <> "http://localhost:" <> show port
    run port $ app ctx


app :: Ctx -> Application
app ctx = cors (const $ Just simpleCorsResourcePolicy { corsRequestHeaders = [HttpTypes.hContentType] }) $ serve appApi $ hoistServer appApi (Handler . ExceptT . try)  $ apiServer ctx
```

Focussing on the highlighted lines, you can see that it first reads the path to the configuration file (you would for instance run this file like so `cabal run betref-server -- config.json`) in line `coreCfgPath <- parseArgs{:haskell}`, then it parses this file `coreCfg <- coreConfigIO coreCfgPath{:haskell}`.

We then see the use of an interesting function `withCfgProviders`. It's type is `withCfgProviders :: GYCoreConfig  -> GYLogNamespace -> (GYProviders -> IO a) -> IO a{:haskell}`, thus, this function first takes our parsed configuration file, then a namespace, finally followed by a continuation `GYProviders -> IO a`. Idea here is that this function will setup a `GYProviders` from the parsed configuration file and send it to this continuation to obtain its result.

## Defining Endpoints

### Shared Context

<Callout type="warning" emoji="ðŸ“ƒ">
Entire code for it is available [here](https://github.com/geniusyield/atlas-examples/tree/main/bet-ref/server-lib/BetRef/Api/Context.hs)
</Callout>

Our endpoints would need an information for our provider, thus we have created the type for it, called `Ctx`. It's usage is made clear by function defined next, `runQuery` which in essence correspond to `ctxRunC` we saw in section on [Integration Tests](./integration-tests.mdx). Reasoning for `runTxI`, `runTxF` & `runTxWithStrategyF` follows similarly.

```haskell
-- | Our Context.
data Ctx = Ctx
  { ctxCoreCfg   :: !GYCoreConfig
  , ctxProviders :: !GYProviders
  }

-- | To run for simple queries, the one which don't requiring building for transaction skeleton.
runQuery :: Ctx -> GYTxQueryMonadNode a -> IO a
runQuery ctx q = do
  let nid       = cfgNetworkId $ ctxCoreCfg ctx
      providers = ctxProviders ctx
  runGYTxQueryMonadNode nid providers q

-- | Wraps our skeleton under `Identity` and calls `runTxF`.
runTxI :: Ctx
       -> [GYAddress]     -- ^ user's used addresses
       -> GYAddress       -- ^ user's change address
       -> GYTxOutRefCbor  -- ^ user's collateral
       -> GYTxMonadNode (GYTxSkeleton v)
       -> IO GYTxBody
runTxI = coerce (runTxF @Identity)

-- | Tries to build for given skeletons wrapped under traversable structure.
runTxF :: Traversable t
       => Ctx
       -> [GYAddress]     -- ^ user's used addresses
       -> GYAddress       -- ^ user's change address
       -> GYTxOutRefCbor  -- ^ user's collateral
       -> GYTxMonadNode (t (GYTxSkeleton v))
       -> IO (t GYTxBody)
runTxF = runTxWithStrategyF GYRandomImproveMultiAsset

-- | Create 'TxBody' from a 'GYTxSkeleton', with the specified coin selection strategy.
runTxWithStrategyF :: Traversable t
                   => GYCoinSelectionStrategy
                   -> Ctx
                   -> [GYAddress]
                   -> GYAddress
                   -> GYTxOutRefCbor
                   -> GYTxMonadNode (t (GYTxSkeleton v))
                   -> IO (t GYTxBody)
runTxWithStrategyF cstrat ctx addrs addr collateral skeleton  = do
  let nid       = cfgNetworkId $ ctxCoreCfg ctx
      providers = ctxProviders ctx
  runGYTxMonadNodeF cstrat nid providers addrs addr (getTxOutRefHex collateral) skeleton
```

## Submit Endpoint

<Callout type="warning" emoji="ðŸ“ƒ">
Entire code for it is available [here](https://github.com/geniusyield/atlas-examples/tree/main/bet-ref/server-lib/BetRef/Api/Tx.hs)
</Callout>

We'll soon see endpoints which will return for unsigned transaction to the browser but assuming that we already have a signed transaction CBOR, let's see how we can define an endpoint to submit it using our provider.

Focussing at `handleSubmitTx` function, we see that we first parse the obtained CBOR text in our `GYTx` type, then we simply call the submit operation for our providor. Then we send back the response letting our front-end know about the transaction fees (which though it would already know when having performed the signing operation, but other fields could also be added here depending upon use case) and the transactoin id.

```haskell
-- | Return type of API when submitting a transaction.
data SubmitTxResponse = SubmitTxResponse
                          { submitTxFee :: !Integer
                          , submitTxId  :: !GYTxId
                          } deriving (Show, Generic, ToJSON, Swagger.ToSchema)

-- | Construct `SubmitTxResponse` return type from the given signed transaction body.
txBodySubmitTxResponse :: GYTxBody -> SubmitTxResponse
txBodySubmitTxResponse txBody = SubmitTxResponse
                                  { submitTxFee = txBodyFee txBody
                                  , submitTxId  = txBodyTxId txBody
                                  }

-- | Type for our Servant API.
type TxAPI =
  "submit" :> ReqBody '[JSON] GYTx :> Post '[JSON] SubmitTxResponse

-- | Serving our API.
handleTx :: Ctx -> ServerT TxAPI IO
handleTx = handleSubmitTx

-- | Handle for submit operation.
handleSubmitTx :: Ctx -> GYTx -> IO SubmitTxResponse
handleSubmitTx ctx tx = do
    void (gySubmitTx (ctxProviders ctx) tx)
    return $ txBodySubmitTxResponse (getTxBody tx)
```

### Transaction Building Endpoints

<Callout type="warning" emoji="ðŸ“ƒ">
Entire code for it is available [here](https://github.com/geniusyield/atlas-examples/tree/main/bet-ref/server-lib/BetRef/Api/BetRef.hs)
</Callout>

At this point, it should be easy to follow the code here. We first define the input type for our endpoint, we also derive its `FromJSON` instance so that we can parse it from JSON that our front-end will send for it and we also derive its `Swagger.ToSchema` instance so as to document our endpoint. Then our endpoint calls the relevant operation which we defined before to get transactoin skeleton, using which we obtain the transaction body with the help of functions such as `runTxI` and return the result (wrapped in our `UnsignedTxResponse` type).

```haskell
-- | Input wrapper around corresponding Plutus type.
data BetRefParams = BetRefParams
  { brpOracleAddress :: !GYAddress
  , brpBetUntil      :: !GYTime
  , brpBetReveal     :: !GYTime
  , brpBetStep       :: !GYValue
  } deriving(Show, Generic, FromJSON, Swagger.ToSchema)

-- | Convert the above `BetRefParams` with corresponding representation defined in our Plutus validator script.
betParamsToScript :: BetRefParams -> Script.BetRefParams
betParamsToScript brp = Script.BetRefParams
  { Script.brpOraclePkh = pubKeyHashToPlutus $ fromJust $ addressToPubKeyHash $ brpOracleAddress brp
  , Script.brpBetUntil = timeToPlutus $  brpBetUntil brp
  , Script.brpBetReveal = timeToPlutus $ brpBetReveal brp
  , Script.brpBetStep = valueToPlutus $  brpBetStep brp
  }

-- | Input parameters for place bet operation.
data PlaceBetRefParams = PlaceBetRefParams
  { pbrUsedAddrs  :: ![GYAddress]
  , pbrChangeAddr :: !GYAddress
  , pbrCollateral :: !GYTxOutRefCbor
  , pbrBetParams  :: !BetRefParams
  , pbrBetGuess   :: !Integer
  , pbrBetAmt     :: !GYValue
  , pbrRefScript  :: !GYTxOutRef
  , pbrPrevBetRef :: !(Maybe GYTxOutRef)
  } deriving (Show, Generic, FromJSON, Swagger.ToSchema)

-- | Input parameters for take bets operation.
data TakeBetRefParams = TakeBetRefParams
  { tbrUsedAddrs         :: ![GYAddress]
  , tbrChangeAddr        :: !GYAddress
  , tbrCollateral        :: !GYTxOutRefCbor
  , tbrBetParams         :: !BetRefParams
  , tbrRefScript         :: !GYTxOutRef
  , tbrPrevBetRef        :: !GYTxOutRef
  , tbrOracleRefInputRef :: !GYTxOutRef
  } deriving (Show, Generic, FromJSON, Swagger.ToSchema)

-- | Input parameters to add for reference script.
data AddRefScriptParams = AddRefScriptParams
  { arsUsedAddrs  :: ![GYAddress]
  , arsChangeAddr :: !GYAddress
  , arsCollateral :: !GYTxOutRefCbor
  , arsPutAddress :: !GYAddress
  , arsBetParams  :: !BetRefParams
  } deriving (Show, Generic, FromJSON, Swagger.ToSchema)

-- | Input parameters to add for reference input.
data AddRefInputParams = AddRefInputParams
  { ariUsedAddrs  :: ![GYAddress]
  , ariChangeAddr :: !GYAddress
  , ariCollateral :: !GYTxOutRefCbor
  , ariPutAddress :: !GYAddress
  , ariBetAnswer  :: !Integer
  } deriving (Show, Generic, FromJSON, Swagger.ToSchema)

-- | Return type for our API endpoints defined here.
data UnsignedTxResponse = UnsignedTxResponse
  { urspTxBodyHex  :: !T.Text           -- ^ Unsigned transaction cbor.
  , urspTxFee      :: !(Maybe Integer)  -- ^ Tx fees.
  , urspUtxoRefIdx :: !(Maybe Word)     -- ^ Some operations might need to show for relevant UTxO generated, this index will let UI know of it. Note that Transaction ID would change after getting signed.
  } deriving (Show, Generic, FromJSON, ToJSON, Swagger.ToSchema)

-- | Construct `UnsignedTxResponse` return type for our endpoint given the transaction body & relevant index for UTxO (if such exists).
unSignedTxWithFee :: GYTxBody -> Maybe Word -> UnsignedTxResponse
unSignedTxWithFee txBody mUtxoRefIdx = UnsignedTxResponse
  { urspTxBodyHex  = T.pack $ txToHex $ unsignedTx txBody
  , urspTxFee      = Just $ txBodyFee txBody
  , urspUtxoRefIdx = mUtxoRefIdx
  }

-- | Type for our Servant API.
type BetRefApi =
       "place"
    :> ReqBody '[JSON] PlaceBetRefParams
    :> Post    '[JSON] UnsignedTxResponse
  :<|> "take"
    :> ReqBody '[JSON] TakeBetRefParams
    :> Post    '[JSON] UnsignedTxResponse
  :<|> "add-ref-script"
    :> ReqBody  '[JSON] AddRefScriptParams
    :> Post     '[JSON] UnsignedTxResponse
  :<|> "add-ref-input"
    :> ReqBody  '[JSON] AddRefInputParams
    :> Post     '[JSON] UnsignedTxResponse

-- | Serving our API.
handleBetRefApi :: Ctx -> ServerT BetRefApi IO
handleBetRefApi ctx =   handlePlaceBet ctx
                   :<|> handleTakeBet ctx
                   :<|> handleAddRefScript ctx
                   :<|> handleOracleRefInput ctx

-- | Handle for place bet operation.
handlePlaceBet :: Ctx -> PlaceBetRefParams -> IO UnsignedTxResponse
handlePlaceBet ctx PlaceBetRefParams{..} = do
  let brp = betParamsToScript pbrBetParams
  validatorAddress <- runQuery ctx (betRefAddress brp)
  txBody <- runTxI ctx pbrUsedAddrs pbrChangeAddr pbrCollateral
              $ placeBet pbrRefScript (betParamsToScript pbrBetParams) (Script.OracleAnswerDatum pbrBetGuess) pbrBetAmt (head pbrUsedAddrs) pbrPrevBetRef
  placeUtxoRef <- case find (\utxo -> utxoAddress utxo == validatorAddress) $ utxosToList $ txBodyUTxOs txBody of
        Nothing -> fail "Shouldn't happen: No reference for placed bet in body"
        Just utxo -> pure $ utxoRef utxo
  pure $ unSignedTxWithFee txBody (Just $ snd $ txOutRefToTuple placeUtxoRef)

-- | Handle for take bets operation.
handleTakeBet :: Ctx -> TakeBetRefParams -> IO UnsignedTxResponse
handleTakeBet ctx TakeBetRefParams{..} = do
  txBody <- runTxI ctx tbrUsedAddrs tbrChangeAddr tbrCollateral
              $ takeBets tbrRefScript (betParamsToScript tbrBetParams) tbrPrevBetRef (head tbrUsedAddrs) tbrOracleRefInputRef
  pure $ unSignedTxWithFee txBody Nothing

-- | Handle for adding reference script.
handleAddRefScript :: Ctx -> AddRefScriptParams  -> IO UnsignedTxResponse
handleAddRefScript  ctx AddRefScriptParams{..} = do
  let validator = betRefValidator' (betParamsToScript arsBetParams)
  txBody <- runTxI ctx arsUsedAddrs arsChangeAddr arsCollateral
              $ pure $ addRefScript' arsPutAddress validator
  let refs   = Limbo.findRefScriptsInBody txBody
  outRef <- case Map.lookup (Some (validatorToScript validator)) refs of
                 Nothing  -> fail "Shouldn't happen: No reference for added Script in body"
                 Just ref -> return ref
  pure $ unSignedTxWithFee txBody (Just $ snd $ txOutRefToTuple outRef)

-- | Handle for adding reference input.
handleOracleRefInput :: Ctx -> AddRefInputParams  -> IO UnsignedTxResponse
handleOracleRefInput  ctx AddRefInputParams{..} = do
  let ourDatumPlutus = Script.OracleAnswerDatum ariBetAnswer
      ourDatumGY = datumFromPlutusData ourDatumPlutus
  txBody <- runTxI ctx ariUsedAddrs ariChangeAddr ariCollateral
              $ pure $ addRefInput' ariPutAddress ourDatumPlutus
  let utxos = utxosToList $ txBodyUTxOs txBody
      ourDatumHash = hashDatum ourDatumGY
      mRefInputUtxo = find (\utxo ->
        case utxoOutDatum utxo of
          GYOutDatumHash dh  -> ourDatumHash == dh
          GYOutDatumInline d -> ourDatumGY == d
          GYOutDatumNone     -> False
        ) utxos
  case mRefInputUtxo of
    Nothing               -> fail "Shouldn't happen: Couldn't find the desired UTxO in Tx outputs"
    Just GYUTxO {utxoRef} -> pure $ unSignedTxWithFee txBody (Just $ snd $ txOutRefToTuple utxoRef)
```

### Wrap-Up

Our both the endpoints file (transaction submition & transaction building) our wrapped up in our [`Api.hs`](https://github.com/geniusyield/atlas-examples/tree/main/bet-ref/server-lib/BetRef/Api/Api.hs) following the usual servant boilerplate.

```haskell
-- | Type for our Servant API.
type Api =
        "tx"  :> TxAPI
  :<|>  "betref" :>  BetRefApi

appApi :: Proxy Api
appApi = Proxy

apiSwagger  :: Swagger
apiSwagger  = toSwagger appApi

apiServer :: Ctx -> ServerT Api IO
apiServer ctx =
       handleTx ctx
  :<|> handleBetRefApi ctx
```

Now coming back to our [`server-main.hs`](https://github.com/geniusyield/atlas-examples/tree/main/bet-ref/server/server-main.hs) file, we can now understand the highlighted code sections which relates to obtaining the Swagger file (generated from `apiSwagger` function above) and running up our servant server.

<Callout>
We follow `simpleCorsResourcePolicy` (also allowing `Content-Type` request header) so that calls by our front-end (which runs on different origin) don't get blocked.
</Callout>

```haskell showLineNumbers {11-12, 20-27}
-- | Getting path for our core configuration.
parseArgs :: IO FilePath
parseArgs = do
  args <- getArgs
  case args of
    coreCfg: _ -> return coreCfg
    _invalidArgument -> fail "Error: wrong arguments, needed a path to the CoreConfig JSON configuration file\n"

main :: IO ()
main = do
  putStrLn "Writing Swagger file ..."
  BL8.writeFile "swagger-api.json" (encodePretty apiSwagger)

  putStrLn "parsing Config ..."
  coreCfgPath <- parseArgs
  coreCfg <- coreConfigIO coreCfgPath  -- Parsing our core configuration.

  putStrLn "Loading Providers ..."
  withCfgProviders coreCfg "api-server"  $ \providers -> do
    let port = 8081
        ctx = Ctx coreCfg providers
    putStrLn $ "Starting server at \n " <> "http://localhost:" <> show port
    run port $ app ctx


app :: Ctx -> Application
app ctx = cors (const $ Just simpleCorsResourcePolicy { corsRequestHeaders = [HttpTypes.hContentType] }) $ serve appApi $ hoistServer appApi (Handler . ExceptT . try)  $ apiServer ctx
```

Next we'll see how to call these endpoints in our front-end!
